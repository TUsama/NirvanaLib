import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import me.modmuss50.mpp.ReleaseType

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
    id "me.modmuss50.mod-publish-plugin" version "0.8.1"

}
def namePattern = "${mod_name}-neoforge-${minecraft_version}"

configurations {
    shade
    implementation.extendsFrom(shade)
}

base {
    archivesName = namePattern
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric

        }
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}



dependencies {
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'



    shade "io.vavr:vavr:0.10.4"
    additionalRuntimeClasspath "io.vavr:vavr:0.10.4"

    shade ("net.neoforged:bus:${neoforge_eventbus_version}") {
        exclude group: "org.ow2.asm"
        exclude group: "net.jodah"
        exclude group: "org.apache.logging.log4j"
        exclude group: "cpw.mods", module: "modlauncher"
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

//https://gist.github.com/SizableShrimp/949e7c219bfc94487a45226b64ac7749
/*
shadowJar {
    archiveClassifier = ''
    configurations = [project.configurations.shade]
    relocate 'net.neoforged.bus', "${project.group}.relocated.net.neoforged.bus"
    finalizedBy 'jar'
}*/

tasks.named('shadowJar', ShadowJar) {
    archiveClassifier = ''
    configurations = [project.configurations.shade]
    relocate 'net.neoforged.bus', "${project.group}.relocated.net.neoforged.bus"
    relocate 'io.vavr', "${project.group}.relocated.io.vavr"
}

assemble.dependsOn shadowJar

publishMods {
    file = tasks.shadowJar.archiveFile
    displayName = file.map { it.asFile.name }
    changelog = file("..\\changelog.md").text.split('\n').collect { string -> {
        if (!string.isBlank()) {
            string + "</br>"
        } else{
            string
        }
    }}.join('\n')
    type = ReleaseType.STABLE
    modLoaders.add("neoforge")
    curseforge {
        accessToken = file("D:\\curseforge-key.txt").text
        projectId = "${project_id}"
        clientRequired = true
        minecraftVersionRange {
            start = "1.21"
            end = "${minecraft_version}"
        }
    }
}